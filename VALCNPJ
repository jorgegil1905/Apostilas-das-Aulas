       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. VALCNPJ.                                                     
       AUTHOR. JOSUE CAMPELO/JANIO PARENTE.                                     
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
       01 WS-CNPJ.                                                              
           05 WS-CNPJ-DIGIT  PICTURE X(14).                                     
           05 WS-CNPJ-VALIDO PICTURE X VALUE "N".                               
                                                                                
       01 WS-SOMATORIO PICTURE 9(5) VALUE 0.                                    
       01 ws-ind-peso  PICTURE 9(2).                                            
       01 WS-DV1       PICTURE 9.                                               
       01 WS-DV2       PICTURE 9.                                               
       01 tab-valores.                                                          
          03  TB-VALORES.                                                       
              05  FILLER picture x(01) value 'A'.                               
              05  FILLER picture 9(03) VALUE 17.                                
              05  FILLER picture x(01) value 'B'.                               
              05  FILLER picture 9(03) VALUE 18.                                
              05  FILLER picture x(01) value 'C'.                               
              05  FILLER picture 9(03) VALUE 19.                                
              05  FILLER picture x(01) value 'D'.                               
              05  FILLER picture 9(03) VALUE 20.                                
              05  FILLER picture x(01) value 'E'.                               
              05  FILLER picture 9(03) VALUE 21.                                
              05  FILLER picture x(01) value 'F'.                               
              05  FILLER picture 9(03) VALUE 22.                                
              05  FILLER picture x(01) value 'G'.                               
              05  FILLER picture 9(03) VALUE 23.                                
              05  FILLER picture x(01) value 'H'.                               
              05  FILLER picture 9(03) VALUE 24.                                
              05  FILLER picture x(01) value 'I'.                               
              05  FILLER picture 9(03) VALUE 25.                                
              05  FILLER picture x(01) value 'J'.                               
              05  FILLER picture 9(03) VALUE 26.                                
              05  FILLER picture x(01) value 'K'.                               
              05  FILLER picture 9(03) VALUE 27.                                
              05  FILLER picture x(01) value 'L'.                               
              05  FILLER picture 9(03) VALUE 28.                                
              05  FILLER picture x(01) value 'M'.                               
              05  FILLER picture 9(03) VALUE 29.                                
              05  FILLER picture x(01) value 'N'.                               
              05  FILLER picture 9(03) VALUE 30.                                
              05  FILLER picture x(01) value 'O'.                               
              05  FILLER picture 9(03) VALUE 31.                                
              05  FILLER picture x(01) value 'P'.                               
              05  FILLER picture 9(03) VALUE 32.                                
              05  FILLER picture x(01) value 'Q'.                               
              05  FILLER picture 9(03) VALUE 33.                                
              05  FILLER picture x(01) value 'R'.                               
              05  FILLER picture 9(03) VALUE 34.                                
              05  FILLER picture x(01) value 'S'.                               
              05  FILLER picture 9(03) VALUE 35.                                
              05  FILLER picture x(01) value 'T'.                               
              05  FILLER picture 9(03) VALUE 36.                                
              05  FILLER picture x(01) value 'U'.                               
              05  FILLER picture 9(03) VALUE 37.                                
              05  FILLER picture x(01) value 'V'.                               
              05  FILLER picture 9(03) VALUE 38.                                
              05  FILLER picture x(01) value 'W'.                               
              05  FILLER picture 9(03) VALUE 39.                                
              05  FILLER picture x(01) value 'X'.                               
              05  FILLER picture 9(03) VALUE 40.                                
              05  FILLER picture x(01) value 'Y'.                               
              05  FILLER picture 9(03) VALUE 41.                                
              05  FILLER picture x(01) value 'Z'.                               
              05  FILLER picture 9(03) VALUE 42.                                
          03  tb-valores-RD REDEFINES tb-valores OCCURS 26 TIMES.               
              05  TB-LET picture x(01).                                         
              05  TB-VAL picture 9(03).                                         
                                                                                
       01 WS-MULTIPLICACAO.                                                     
           05 WS-MULTIPLICACAO-RESULTADO PICTURE 9(5).                          
                                                                                
       01 WS-CONSTANTES.                                                        
           05 WS-CONSTANTE-PESO1 PIC X(12) VALUE '543298765432'.                
           05 WS-CONSTANTE-PESO2 PIC X(13) VALUE '6543298765432'.               
                                                                                
       01 WS-RESTO     PICTURE 9(2).                                            
       01 WS-QUOCIENTE PICTURE 9(5).                                            
       01 WS-LETRA     PICTURE X.                                               
       01 WS-LETRA-NUM PICTURE 9(5).                                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *    display 'INFORME CNPJ A SER VALIDADO'.                               
      *    ACCEPT WS-CNPJ-DIGIT.                                                
           MOVE 'JOSUE029000115' TO WS-CNPJ-DIGIT.                              
      *    MOVE 'QPZBRB5LYXWW93' TO WS-CNPJ-DIGIT.                              
      *    DISPLAY 'INFORME O CNPJ COM OS DIGITOS'.                             
      *    ACCEPT WS-CNPJ-DIGIT FROM CONSOLE.                                   
           PERFORM VALIDA-DIGITO-VERIFICADOR1.                                  
           PERFORM VALIDA-DIGITO-VERIFICADOR2.                                  
           IF WS-DV1 = FUNCTION NUMVAL(WS-CNPJ(13:1)) AND                       
              WS-DV2 = FUNCTION NUMVAL(WS-CNPJ(14:1))                           
               MOVE "S" TO WS-CNPJ-VALIDO                                       
           END-IF                                                               
           DISPLAY WS-CNPJ-DIGIT " CNPJ Valido? " WS-CNPJ-VALIDO                
           STOP RUN.                                                            
                                                                                
       VALIDA-DIGITO-VERIFICADOR1.                                              
           MOVE 0 TO WS-SOMATORIO.                                              
           PERFORM VARYING ws-ind-peso FROM 1 BY 1 UNTIL ws-ind-peso > 12       
               PERFORM VERIFICA-NUMERO                                          
      *        DISPLAY WS-LETRA-NUM                                             
               COMPUTE WS-MULTIPLICACAO-RESULTADO =                             
                   WS-LETRA-NUM *                                               
                   FUNCTION NUMVAL(WS-CONSTANTE-PESO1(ws-ind-peso:1))           
               ADD WS-MULTIPLICACAO-RESULTADO TO WS-SOMATORIO                   
           END-PERFORM                                                          
           DIVIDE WS-SOMATORIO BY 11 GIVING WS-QUOCIENTE REMAINDER              
                                                         WS-RESTO               
           IF WS-RESTO = 0 OR WS-RESTO = 1                                      
               MOVE 0 TO WS-DV1                                                 
           ELSE                                                                 
               COMPUTE WS-DV1 = 11 - WS-RESTO                                   
           END-IF.                                                              
                                                                                
       VALIDA-DIGITO-VERIFICADOR2.                                              
           MOVE 0 TO WS-SOMATORIO.                                              
           PERFORM VARYING ws-ind-peso FROM 1 BY 1 UNTIL ws-ind-peso > 13       
               PERFORM VERIFICA-NUMERO                                          
      *        DISPLAY WS-LETRA-NUM                                             
               COMPUTE WS-MULTIPLICACAO-RESULTADO =                             
                   WS-LETRA-NUM *                                               
                   FUNCTION NUMVAL(WS-CONSTANTE-PESO2(ws-ind-peso:1))           
               ADD WS-MULTIPLICACAO-RESULTADO TO WS-SOMATORIO                   
           END-PERFORM                                                          
           DIVIDE WS-SOMATORIO BY 11 GIVING WS-QUOCIENTE REMAINDER              
                                                         WS-RESTO               
           IF WS-RESTO = 0 OR WS-RESTO = 1                                      
               MOVE 0 TO WS-DV2                                                 
           ELSE                                                                 
               COMPUTE WS-DV2 = 11 - WS-RESTO                                   
           END-IF.                                                              
                                                                                
       VERIFICA-NUMERO.                                                         
           IF WS-CNPJ(ws-ind-peso:1) IS NUMERIC                                 
               MOVE WS-CNPJ(ws-ind-peso:1)                                      
                    TO WS-LETRA-NUM                                             
           ELSE                                                                 
               MOVE WS-CNPJ(ws-ind-peso:1)                                      
                    TO WS-LETRA                                                 
      *********************************************************                 
      * AS LETRAS MAIUSCULAS NA TABELA EBCDIC ESTAO EM 3 BLOCOS                 
      * DE A(193) ATE I(201)                                                    
      * DE J(209) ATE R(217)                                                    
      * DE S(226) ATE Z(233)                                                    
      *********************************************************                 
      * A FUNCAO ORD() CONVERTE PARA UM VALOR ACRESCIDO DE 1                    
      * PORISSO O DECREMENTO DE 1 PARA FICAR IGUAL A TABELA                     
      *********************************************************                 
      * CONFORME A NORMA TECNICA, A=17 B=18 ... Z=42                            
      *********************************************************                 
               COMPUTE WS-LETRA-NUM = FUNCTION ORD(WS-LETRA) - 1                
                IF WS-LETRA-NUM IS GREATER THAN 192 AND                         
                                   LESS    THAN 202                             
                   COMPUTE WS-LETRA-NUM = WS-LETRA-NUM - 176                    
                END-IF                                                          
                IF WS-LETRA-NUM IS GREATER THAN 208 AND                         
                                   LESS    THAN 218                             
                   COMPUTE WS-LETRA-NUM = WS-LETRA-NUM - 183                    
                END-IF                                                          
                IF WS-LETRA-NUM IS GREATER THAN 225 AND                         
                                   LESS    THAN 234                             
                   COMPUTE WS-LETRA-NUM = WS-LETRA-NUM - 191                    
                END-IF                                                          
           END-IF.                                                              
       END PROGRAM VALCNPJ.                                                     
                                                                                
